plugins {
  // Do not upgrade
  id 'org.inferred.processors' version '3.6.0'
  id 'com.github.ben-manes.versions' version '0.53.0'  //gradle dependencyUpdates
}

ext {
  libVersion = '0.1.2'
}

println("Building version ${libVersion}")

subprojects {
  group = 'com.navigo3'
  version = rootProject.ext.libVersion

  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'org.inferred.processors'
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'

  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21

  repositories {
    mavenCentral()
  }

  configurations {
    deployerJars
  }

  

  jar {
    archiveVersion.set(project.version)
  }

  eclipse {
      project {
          resourceFilter {
              appliesTo = 'FOLDERS'
              type = 'EXCLUDE_ALL'
              matcher {
                  id = 'org.eclipse.ui.ide.multiFilter'
                  arguments = '1.0-name-matches-false-false-build'
              }
          }
      }
  }
  
  task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    archiveVersion.set(jar.archiveVersion.get())
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourcesJar
  }

  publishing {
    repositories {
      maven {
        name = 'myRepo'
        url = "file://${projectDir}/../repo"
      }
    }
  }
}

configure(subprojects.findAll {it.name != 'dry-api-interface'}) {
  dependencies {
    processor group: 'org.immutables', name: 'value', version: '2.11.6'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
    implementation group: 'commons-io', name: 'commons-io', version: '2.20.0'
  }
}
